apply plugin: 'org.ajoberstar.grgit'
apply plugin: 'com.vanniktech.maven.publish'

/**
 * Clone AOSP's prebuilts repo to get a prebuilt layoutlib jar. This big repo that takes a long time to clone!
 *
 * https://android.googlesource.com/platform/prebuilts/studio/layoutlib/+/refs/tags/studio-3.2.1/PREBUILT
 */

ext.repoDir = new File("$rootProject.buildDir/prebuilts/studio/layoutlib")

tasks.register('cloneLayoutlib') {
  outputs.dir(repoDir)

  doFirst {
    // Gradle aggressively creates outputs directories, which interferes with the git clone check
    if (repoDir.exists() && !repoDir.list()) {
      repoDir.delete()
    }
    if (!repoDir.exists()) {
      logger.warn('Cloning prebuilt layoutlib: this make take a few minutes...')
      grgit.clone {
        dir = repoDir
        uri = "https://android.googlesource.com/platform/prebuilts/studio/layoutlib"
      }
      logger.warn('Cloned prebuilt layoutlib.')
    }

    def repo = grgit.open {
      dir = repoDir
    }
    repo.checkout {
      branch = layoutLibPrebuiltSha
    }
  }
}

/**
 * Publish layoutlib.jar to Maven Central with Paparazzi coordinates.
 */
publishing {
  publications {
    layoutLib(MavenPublication) {
      artifact(tasks.
          named("cloneLayoutlib").
          map {
            new File(it.outputs.files.getSingleFile(), 'data/layoutlib-jdk11.jar')
          }
      )

      version = versions.layoutlib
    }
  }
}

